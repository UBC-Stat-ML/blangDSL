grammar ca.ubc.stat.blang.BlangDsl with org.eclipse.xtext.xbase.Xbase

generate blangDsl "http://www.ubc.ca/stat/blang/BlangDsl"

BlangModel:
	{BlangModel}
	('package' name=QualifiedName)?
	importSection=XImportSection?
	('model' '{'
		vars=Vars
		laws = Laws?
	'}')?
	;
	
Vars:
	{Vars}
	(randomVars += Random)*
	;

Random:
	'random' type=JvmTypeReference name=ValidID
;

VarDecl:
	type=JvmTypeReference? name=ValidID '=' right=XExpression
;

Laws:
	{Laws}
	'laws' '{'
	(modelComponents += ModelComponent)*
	'}'
;

// y | Real mean = mu ~ Normal(mean, [mean.doubleValue ** 2])
ModelComponent:
	(name = ValidID) '|' (deps += Dependency) + '~' (right = Distribution)
;

Dependency:
	(type = JvmTypeReference) (name = ValidID) '=' (init = ValidID)
;

Distribution:
	(clazz = JvmTypeReference) '('
		(param += Param) ( ',' param += Param)*
		')'
;

Param:
	LazyParam | ConstParam
;

ConstParam:
    id = ValidID
;

LazyParam:
	expr = XClosure
;
