grammar ca.ubc.stat.blang.BlangDsl with org.eclipse.xtext.xbase.Xbase

generate blangDsl "http://www.ubc.ca/stat/blang/BlangDsl"

import "http://www.eclipse.org/xtext/common/JavaVMTypes" as jvmTypes

BlangModel:
	{BlangModel}
	('package' package=QualifiedName)?
	importSection=XImportSection?
	(annotations += BlangAnnotation)*
	'model' name=ID '{'
    (variableDeclarations += VariableDeclaration)*
    'laws' '{' (lawNodes += LawNode)* '}'
    (variableDeclarations += VariableDeclaration)*
	'}'
;

// note: we are avoiding XbaseWithAnnotation since it created a problem 
// indirectly (see Issue #24 in blangSDK)
BlangAnnotation:
  '@' type=JvmTypeReference ('(' arguments += QualifiedName (',' arguments += QualifiedName)* ')')? 
;
	
VariableDeclaration:
	(variableType = VariableType) type=JvmTypeReference ( components += VariableDeclarationComponent) (',' components += VariableDeclarationComponent)*
;

VariableDeclarationComponent:
  name = ValidID
  ('?:' varInitBlock = XExpression)?
;

enum VariableType:   // TODO: poor name: pick something else like randomnessStatus
  RANDOM = 'random' | PARAM = 'param'
;

LawNode:
  'for' ForLoop | 'indicator' IndicatorDeclaration | 'logf' LogScaleFactorDeclaration | (=> InstantiatedDistribution)
;

IndicatorDeclaration:
  {IndicatorDeclaration}
  contents=FactorDeclaration
;

LogScaleFactorDeclaration:
  {LogScaleFactorDeclaration}
  contents=FactorDeclaration
;

ForLoop:
  '('
    iteratorType = JvmTypeReference
    name = ValidID
    ':'
    iteratorRange = XExpression
  ')' '{'
    (loopBody += LawNode)*
  '}'
;

// indicator(variance) = variance.doubleValue > 0
FactorDeclaration:
  '(' ((dependencies += Dependency) (',' dependencies += Dependency)* )? ')' 
  (factorBody = XBlockExpression)
;

// y1, y2 | Matrix mean = means.get(z), variance ~ BivariateNormal(mean, [variance ** 2])
InstantiatedDistribution:
  // random variables generated
	( generatedVariables += XExpression  (',' generatedVariables += XExpression)* )?
	// dependencies on other variables
	('|' (dependencies += Dependency) (',' dependencies += Dependency)* )?  
	// type of the distribution
	'~' distributionType = [BlangModel|QualifiedName] 
	// arguments in the constructor of the distribution
	'('
	  ( (arguments += XExpression) ( ',' arguments += XExpression)* )?
	')'
;

Dependency:
  InitializerDependency | SimpleDependency
;

InitializerDependency:
	(type = JvmTypeReference) (name = ValidID) '=' (init = XExpression)
;

SimpleDependency:
  variable = [VariableDeclarationComponent]
;
