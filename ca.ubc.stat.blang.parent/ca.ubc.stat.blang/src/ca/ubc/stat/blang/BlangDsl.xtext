grammar ca.ubc.stat.blang.BlangDsl with org.eclipse.xtext.xbase.Xbase

generate blangDsl "http://www.ubc.ca/stat/blang/BlangDsl"

BlangModel:
	{BlangModel}
	('package' name=QualifiedName)?
	importSection=XImportSection?
	('model' '{'
		vars=Vars
		laws = Laws?
	'}')?
	;
	
Vars:
	{Vars}
	((randomVars += Random) | (paramVars += ParamVar) | (consts += Const))*
	;

Random:
	'random' type=JvmTypeReference name=ValidID
;

ParamVar:
    'param' type=JvmTypeReference name=ValidID
;

Const:
    'const' type=JvmTypeReference name=ValidID '=' right=XExpression
;

VarDecl:
	type=JvmTypeReference? name=ValidID '=' right=XExpression
;

Laws:
	{Laws}
	'laws' '{'
	(modelComponents += ModelComponent)*
	'}'
;

ModelComponent:
    SupportFactor | LogScaleFactor | ModelParam
;

// indicator(variance) = variance.doubleValue > 0
SupportFactor:
    'indicator' '(' (name = ValidID) ')' '=' (expr = XBlockExpression)
;

// logf(variance) = -0.5 * ( log(variance.doubleValue) + LOG2PI )
LogScaleFactor:
    'logf' '(' (name = ValidID) ')' '=' (expr = XBlockExpression)
;

// y | Real mean = mu ~ Normal(mean, [mean.doubleValue ** 2])
ModelParam:
	(name = ValidID) '|' (deps += Dependency) + '~' (right = Distribution)
;

Dependency:
	(type = JvmTypeReference) (name = ValidID) '=' (init = ValidID)
;

Distribution:
	(clazz = JvmTypeReference) '('
		(param += Param) ( ',' param += Param)*
		')'
;

Param:
	LazyParam | ConstParam
;

ConstParam:
    id = ValidID
;

LazyParam:
	expr = XClosure
;
